

* -------------------- ENERGY CAPACITY ---------------------

* ------------- ELECTRICITY CAPACITY --------------

** EXOGENOUS ELECTRICITY CAPACITY

G_CAP_YCRAF_scenarios(scenarios,Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'EXOGENOUS','GW')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE))=
IOF0001*GKFX(Y,IA,IGE)/(1$((NOT IGESTO(IGE)) AND (NOT IGESTOS(IGE)))+GDATA(IGE,'GDSTOHUNLD')$((IGESTO(IGE)) OR IGESTOS(IGE)))
;


** ENDOGENOUS ELECTRIC CAPACITY ACCUMULATED INVESTMENTS
G_CAP_YCRAF_scenarios(scenarios,Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'ENDOGENOUS','GW')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE))=
IOF0001*VGKNACCUMNET.L(Y,IA,IGE)/(1$((NOT IGESTO(IGE)) AND (NOT IGESTOS(IGE)))+GDATA(IGE,'GDSTOHUNLD')$((IGESTO(IGE)) OR IGESTOS(IGE)))
;

** ENDOGENOUS ELECTRICITY CAPACITY ACCUMULATED DECOMMISSIONING
$ifi %DECOM%==yes G_CAP_YCRAF_scenarios(scenarios,Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'DECOMMISSIONING','GW')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,IGE))=
$ifi %DECOM%==yes -IOF0001*VDECOM_EXO_ACCUM.L(Y,IA,IGE)/(1$((NOT IGESTO(IGE)) AND (NOT IGESTOS(IGE)))+GDATA(IGE,'GDSTOHUNLD')$((IGESTO(IGE)) OR IGESTOS(IGE)))
;


* ------------- END OF ELECTRICITY CAPACITY --------------

* ------------- HEAT CAPACITY --------------

** EXOGENOUS HEAT CAPACITY

G_CAP_YCRAF_scenarios(scenarios,Y,C,IR,IA,IGH,FFF,'HEAT',TECH_TYPE,'EXOGENOUS','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE))=
IOF0001*GKFX(Y,IA,IGH)/((1$((NOT IGHSTO(IGH)) AND (NOT IGHSTOS(IGH)))+GDATA(IGH,'GDSTOHUNLD')$((IGHSTO(IGH)) OR IGHSTOS(IGH)))*(GDATA(IGH,'GDCB')$IGBPR(IGH)+ (GDATA(IGH,'GDCB')+GDATA(IGH,'GDCV'))$IGEXT(IGH)+1$(NOT (IGBPR(IGH) OR IGEXT(IGH)))))
;


** ENDOGENOUS HEAT CAPACITY ACCUMULATED INVESTMENTS
G_CAP_YCRAF_scenarios(scenarios,Y,C,IR,IA,IGH,FFF,'HEAT',TECH_TYPE,'ENDOGENOUS','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE))=
IOF0001*VGKNACCUMNET.L(Y,IA,IGH)/((1$((NOT IGHSTO(IGH)) AND (NOT IGHSTOS(IGH)))+GDATA(IGH,'GDSTOHUNLD')$((IGHSTO(IGH)) OR IGHSTOS(IGH)))*(GDATA(IGH,'GDCB')$IGBPR(IGH)+ (GDATA(IGH,'GDCB')+GDATA(IGH,'GDCV'))$IGEXT(IGH)+1$(NOT (IGBPR(IGH) OR IGEXT(IGH)))))
;

** ENDOGENOUS HEAT CAPACITY ACCUMULATED DECOMMISSIONING
$ifi %DECOM%==yes G_CAP_YCRAF_scenarios(scenarios,Y,C,IR,IA,IGH,FFF,'HEAT',TECH_TYPE,'DECOMMISSIONING','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,IGH))=
$ifi %DECOM%==yes -IOF0001*VDECOM_EXO_ACCUM.L(Y,IA,IGH)/((1$((NOT IGHSTO(IGH)) AND (NOT IGHSTOS(IGH)))+GDATA(IGH,'GDSTOHUNLD')$((IGHSTO(IGH)) OR IGHSTOS(IGH)))*(GDATA(IGH,'GDCB')$IGBPR(IGH)+ (GDATA(IGH,'GDCB')+GDATA(IGH,'GDCV'))$IGEXT(IGH)+1$(NOT (IGBPR(IGH) OR IGEXT(IGH)))))
;

* ------------- END OF HEAT CAPACITY --------------

* ---------------- END OF ENERGY CAPACITY -----------------

* -------------------- ENERGY STORAGE ---------------------

* ------------- ELECTRICITY STORAGE --------------

** EXOGENOUS ELECTRICITY STORAGE

G_STO_YCRAF_scenarios(scenarios,Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'EXOGENOUS','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND (IGESTO(IGE) OR IGESTOS(IGE)) )=
IOF0001*GKFX(Y,IA,IGE)
;

** ENDOGENOUS ELECTRIC STORAGE ACCUMULATED INVESTMENTS
G_STO_YCRAF_scenarios(scenarios,Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'ENDOGENOUS','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND (IGESTO(IGE) OR IGESTOS(IGE)) )=
IOF0001*VGKNACCUMNET.L(Y,IA,IGE)
;

** ENDOGENOUS ELECTRICITY STORAGE ACCUMULATED DECOMMISSIONING
$ifi %DECOM%==yes G_STO_YCRAF_scenarios(scenarios,Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'DECOMMISSIONING','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,IGE) AND (IGESTO(IGE) OR IGESTOS(IGE)) )=
$ifi %DECOM%==yes -IOF0001*VDECOM_EXO_ACCUM.L(Y,IA,IGE)
;


* ------------- END OF ELECTRICITY CAPACITY --------------

* ------------- HEAT STORAGE --------------

** EXOGENOUS HEAT STORAGE

G_STO_YCRAF_scenarios(scenarios,Y,C,IR,IA,IGH,FFF,'HEAT',TECH_TYPE,'EXOGENOUS','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND (IGHSTO(IGH) OR IGHSTOS(IGH)))=
IOF0001*GKFX(Y,IA,IGH)
;

** ENDOGENOUS HEAT STORAGE ACCUMULATED INVESTMENTS
G_STO_YCRAF_scenarios(scenarios,Y,C,IR,IA,IGH,FFF,'HEAT',TECH_TYPE,'ENDOGENOUS','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND (IGHSTO(IGH) OR IGHSTOS(IGH)))=
IOF0001*VGKNACCUMNET.L(Y,IA,IGH)
;

** ENDOGENOUS HEAT STORAGE ACCUMULATED DECOMMISSIONING
$ifi %DECOM%==yes G_STO_YCRAF_scenarios(scenarios,Y,C,IR,IA,IGH,FFF,'HEAT',TECH_TYPE,'DECOMMISSIONING','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,IGH) AND (IGHSTO(IGH) OR IGHSTOS(IGH)))=
$ifi %DECOM%==yes -IOF0001*VDECOM_EXO_ACCUM.L(Y,IA,IGH)
;

* ------------- END OF HEAT STORAGE --------------

* ---------------- END OF ENERGY STORAGE -----------------


* -------------------- ENERGY PRODUCTION ---------------------



** ELECTRICITY PRODUCTION ANNUALLY
PRO_YCRAGF_scenarios(scenarios,Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'TWh')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE))=
       SUM((S,T),IHOURSINST(S,T)*VGE_T.L(Y,IA,IGE,S,T))/IOF1000000;


** heat PRODUCTION ANNUALY
PRO_YCRAGF_scenarios(scenarios,Y,C,IR,IA,IGH,FFF,'HEAT',TECH_TYPE,'TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE))=
       SUM((S,T),IHOURSINST(S,T)*VGH_T.L(Y,IA,IGH,S,T))/IOF1000000;

* ---------------- END OF ENERGY PRODUCTION -----------------


* -------------------- FUEL CONSUMPTION ---------------------


** ANNUAL FUEL CONSUMPTION
F_CONS_YCRA_scenarios(scenarios,Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE))=
       SUM((S,T),IHOURSINST(S,T)*VGF_T.L(Y,IA,G,S,T))/IOF1000000;


* ---------------- END OF FUEL CONSUMPTION -----------------


* ------------------------- ENERGY DEMAND ----------------------

* ------------- ELECTRICITY DEMAND --------------


*ANNUAL DEMAND
** EXOGENOUS DEMAND

EL_DEMAND_YCR_scenarios(scenarios,Y,C,IR,'EXOGENOUS','TWh')$CCCRRR(C,IR)=
         SUM((S,T,DEUSER),IHOURSINST(S,T)*IDE_T(Y,IR,DEUSER,S,T))/(1-DISLOSS_E(IR))/IOF1000000;

** ENDOGENOUS DEMAND

EL_DEMAND_YCR_scenarios(scenarios,Y,C,IR,'ENDO_HEATPUMP','TWh')$CCCRRR(C,IR)=
* HEAT PUMPS CONSUMPTION
         SUM((S,T,IA)$RRRAAA(IR,IA), SUM(IGETOH$(IAGK_HASORPOT(Y,IA,IGETOH) AND GDATA(IGETOH,'GDTECHGROUP') EQ HEATPUMP), IHOURSINST(S,T)*VGE_T.L(Y,IA,IGETOH,S,T)))/IOF1000000
;

EL_DEMAND_YCR_scenarios(scenarios,Y,C,IR,'ENDO_ELBOILER','TWh')$CCCRRR(C,IR)=
* HEAT PUMPS CONSUMPTION
         SUM((S,T,IA)$RRRAAA(IR,IA), SUM(IGETOH$(IAGK_HASORPOT(Y,IA,IGETOH) AND GDATA(IGETOH,'GDTECHGROUP') EQ BOILER), IHOURSINST(S,T)*VGE_T.L(Y,IA,IGETOH,S,T)))/IOF1000000
;

EL_DEMAND_YCR_scenarios(scenarios,Y,C,IR,'ENDO_INTRASTO','TWh')$CCCRRR(C,IR)=
* INTRASEASONAL ELECTRICITY STORAGE LOSS
         +SUM((S,T,IA)$RRRAAA(IR,IA),SUM(IGESTO$IAGK_HASORPOT(Y,IA,IGESTO),IHOURSINST(S,T)*(VESTOLOADT.L(Y,IA,IGESTO,S,T)-VGE_T.L(Y,IA,IGESTO,S,T))))/IOF1000000
;

EL_DEMAND_YCR_scenarios(scenarios,Y,C,IR,'ENDO_INTERSTO','TWh')$CCCRRR(C,IR)=
* INTERSEASONAL ELECTRICITY STORAGE LOSS
         +SUM((S,T,IA)$RRRAAA(IR,IA),SUM(IGESTOS$IAGK_HASORPOT(Y,IA,IGESTOS),IHOURSINST(S,T)*(VESTOLOADTS.L(Y,IA,IGESTOS,S,T)-VGE_T.L(Y,IA,IGESTOS,S,T))))/IOF1000000
;

EL_DEMAND_YCR_scenarios(scenarios,Y,C,IR,'ENDO_EV','TWh')$CCCRRR(C,IR)=
         SUM((S,T),IHOURSINST(S,T)*(VEV_VNETCHARGE_BEV.L(Y,IR,S,T) +VEV_VNETCHARGE_PHEV.L(Y,IR,S,T)))/IOF1000000;

* ----------- END OF ELECTRICITY DEMAND --------------

* ------------- HEAT DEMAND --------------


*ANNUAL DEMAND
** EXOGENOUS DEMAND

H_DEMAND_YCRA_scenarios(scenarios,Y,C,IR,IA,'EXOGENOUS','TWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=
         SUM((S,T,DHUSER),IHOURSINST(S,T)*IDH_T(Y,IA,DHUSER,S,T))/(1-DISLOSS_H(IA))/IOF1000000;

** ENDOGENOUS DEMAND

H_DEMAND_YCRA_scenarios(scenarios,Y,C,IR,IA,'ENDO_INTRASTO','TWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=

* INTRASEASONAL HEAT STORAGE LOSS
        SUM((S,T,IGHSTO)$IAGK_HASORPOT(Y,IA,IGHSTO),IHOURSINST(S,T)*(VHSTOLOADT.L(Y,IA,IGHSTO,S,T)-VGH_T.L(Y,IA,IGHSTO,S,T)))/IOF1000000

;

H_DEMAND_YCRA_scenarios(scenarios,Y,C,IR,IA,'ENDO_INTERSTO','TWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=

* INTERSEASONAL HEAT STORAGE LOSS
        + SUM((S,T,IGHSTOS)$IAGK_HASORPOT(Y,IA,IGHSTOS),IHOURSINST(S,T)*(VHSTOLOADTS.L(Y,IA,IGHSTOS,S,T)-VGH_T.L(Y,IA,IGHSTOS,S,T)))/IOF1000000
;

* ----------- END OF HEAT DEMAND --------------

* -------------------- END OF ENERGY DEMAND -----------------------


* -------------------- TRANSMISSION CAPACITY -----------------------

** EXOGENOUS ELECTRICITY CAPACITY

X_CAP_YCR_scenarios(scenarios,Y,C,IR,IRI,'EXOGENOUS','GW')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=
IOF0001*XKFX(Y,IR,IRI)
;


** ENDOGENOUS ELECTRIC CAPACITY

$ifi not %TransInvest%==yes $goto Transmission_investments_end
X_CAP_YCR_scenarios(scenarios,Y,C,IR,IRI,'ENDOGENOUS','GW')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=
IOF0001*VXKNACCUMNET.L(Y,IR,IRI)
;
$label  Transmission_investments_end


* -------------------- END OF TRANSMISSION CAPACITY -----------------------

* -------------------- TRANSMISSION FLOW -----------------------




** ANNUAL TRANSMISSION FLOW
X_FLOW_YCR_scenarios(scenarios,Y,C,IR,IRI,'TWh')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=
SUM((S,T),IHOURSINST(S,T)*VX_T.L(Y,IR,IRI,S,T))/IOF1000000;
;



* -------------------- END OF TRANSMISSION FLOW -----------------------

$ifi NOT %HEATTRANS%==yes $goto no_heattrans
* -------------------- HEAT TRANSMISSION CAPACITY -----------------------

** EXOGENOUS HEAT TRANSMISSION CAPACITY

XH_CAP_YCA_scenarios(scenarios,Y,C,IA,IAI,'EXOGENOUS','GW')$(ICA(C,IA) AND IXHK_HASORPOT(Y,IA,IAI))=
IOF0001*XHKFX(Y,IA,IAI)
;


** ENDOGENOUS HEAT TRANSMISSION CAPACITY

$ifi not %HeatTransInvest%==yes $goto Heat_Transmission_investments_end
XH_CAP_YCA_scenarios(scenarios,Y,C,IA,IAI,'ENDOGENOUS','GW')$(ICA(C,IA) AND IXHK_HASORPOT(Y,IA,IAI))=
IOF0001*VXHKNACCUMNET.L(Y,IA,IAI)
;
$label  Heat_Transmission_investments_end


* -------------------- END OF HEAT TRANSMISSION CAPACITY -----------------------

* -------------------- HEAT TRANSMISSION FLOW -----------------------


** ANNUAL HEAT TRANSMISSION FLOW
XH_FLOW_YCA_scenarios(scenarios,Y,C,IA,IAI,'TWh')$(ICA(C,IA) AND IXHK_HASORPOT(Y,IA,IAI))=
SUM((S,T),IHOURSINST(S,T)*VXH_T.L(Y,IA,IAI,S,T))/IOF1000000;
;

* -------------------- END OF HEAT TRANSMISSION FLOW -----------------------
$label no_heattrans

* ---------------------- ECONOMIC OUTPUT ----------------------

* ------------- COMMODITY PRICES --------------

** ELECTRICITY PRICES

*HOURLY ELECTRICITY PRICE

EL_PRICE_YCRST_scenarios(scenarios,Y,C,IR,S,T,'Money_per_MWh')$CCCRRR(C,IR)=OMONEY*QEEQ.M(Y,IR,S,T)
$ifi %RollingSeasons%==yes         /IHOURSINTROLLINGSEASON(S,T)
$ifi not %RollingSeasons%==yes     /IHOURSINST(S,T)
/IDISCOUNTFACTOR(Y)/IWEIGHTY(Y);

*AVERAGE ELECTRICITY PRICE

EL_PRICE_YCR_scenarios(scenarios,Y,C,IR,'AVERAGE','Money_per_MWh')$CCCRRR(C,IR)=
SUM((S,T),EL_PRICE_YCRST_scenarios(scenarios,Y,C,IR,S,T,'Money_per_MWh')/(IWEIGHSUMS*IWEIGHSUMT/(WEIGHT_S(S)*WEIGHT_T(T))));



** END OF ELECTRICITY PRICES

** heatING PRICES

*HOURLY heatING PRICE

H_PRICE_YCRAST_scenarios(scenarios,Y,C,IR,IA,S,T,'Money_per_MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND SUM(DHUSER, IDH_SUMST(IA,DHUSER)))=OMONEY*QHEQ.M(Y,IA,S,T)
$ifi %RollingSeasons%==yes         /IHOURSINTROLLINGSEASON(S,T)
$ifi not %RollingSeasons%==yes     /IHOURSINST(S,T)
/IDISCOUNTFACTOR(Y)/IWEIGHTY(Y);

*AVERAGE heatING PRICE

H_PRICE_YCRA_scenarios(scenarios,Y,C,IR,IA,'AVERAGE','Money_per_MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND SUM(DHUSER, IDH_SUMST(IA,DHUSER)))=
SUM((S,T),H_PRICE_YCRAST_scenarios(scenarios,Y,C,IR,IA,S,T,'Money_per_MWh')/(IWEIGHSUMS*IWEIGHSUMT/(WEIGHT_S(S)*WEIGHT_T(T))));



** END OF heatING PRICES


* ------------- END OF COMMODITY PRICES --------------


* ------------- COSTS --------------

* -- GENERATION COSTS -----

** GENERATION INVESTMENTS

ECO_G_YCRAG_scenarios(scenarios,Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_CAPITAL_COSTS','Mmoney')$(CCCRRR(C,IR) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and RRRAAA(IR,IA))=
         OMONEY*SUM(IYALIAS2$((ORD(IYALIAS2) LE ORD(Y)) AND IAGKNY(IYALIAS2,IA,G) AND (NOT IGKN_ES(G))),IYHASANNUITYG(IYALIAS2,Y,G)*VGKN.L(IYALIAS2,IA,G)*GINVCOST(IA,G)*ANNUITYCG(C,G));

** GENEARTION FIXED COSTS

ECO_G_YCRAG_scenarios(scenarios,Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_FIXED_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and IAGK_HASORPOT(Y,IA,G))=
         OMONEY/IOF1000*GOMFCOST(IA,G)*(
         GKFX(Y,IA,G)
         +VGKNACCUMNET.L(Y,IA,G)
$ifi %DECOM%==yes         -VDECOM_EXO_ACCUM.L(Y,IA,G)$IGDECOMEXOPOT(Y,IA,G)
         )
;

** GENERATION O&M COSTS

ECO_G_YCRAG_scenarios(scenarios,Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_OPERATIONAL_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and IAGK_HASORPOT(Y,IA,G))=
   OMONEY/IOF1000000*
     ((GOMVCOSTO(IA,G)*
      (
   +   SUM((S,T), IHOURSINST(S,T) * VGE_T.L(Y,IA,G,S,T))$IAGK_HASORPOT(Y,IA,G)))$IGKE(G)

   +  (GOMVCOSTO(IA,G) * GDATA(G,'GDCV') *
      (
   +   SUM((S,T), IHOURSINST(S,T) * VGH_T.L(Y,IA,G,S,T))$IAGK_HASORPOT(Y,IA,G)))$IGKH(G)

   +  (GOMVCOSTIN(IA,G)*
      (
   +   SUM((S,T), IHOURSINST(S,T) * VGF_T.L(Y,IA,G,S,T))$IAGK_HASORPOT(Y,IA,G)))
     ) ;

** GENERATION FUEL COSTS

ECO_G_YCRAG_scenarios(scenarios,Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_FUEL_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and IAGK_HASORPOT(Y,IA,G))=
   OMONEY*IOF3P6/IOF1000000*(
                 FUELPRICE(Y,IA,FFF)
$ifi not %STEPWISEPRICE%==yes $goto No_STEPWISEPRICE
                 + (SUM((STEPWISEPRICE_L,STEPWISEPRICE_FFF(FFF))$ISTEPWISEPRICE_F(Y,C,STEPWISEPRICE_FFF),
                 STEPWISEPRICE_FUELPRICE_STEP(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF)*VSTEPWISEPRICE_VGF_T_STEP.L(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF))
                 /SUM((STEPWISEPRICE_L,STEPWISEPRICE_FFF(FFF)),VSTEPWISEPRICE_VGF_T_STEP.L(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF)))$(SUM(STEPWISEPRICE_FFF(FFF),ISTEPWISEPRICE_F(Y,C,STEPWISEPRICE_FFF)) AND SUM((STEPWISEPRICE_L,STEPWISEPRICE_FFF(FFF)),VSTEPWISEPRICE_VGF_T_STEP.L(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF)))
$label No_STEPWISEPRICE
                 )*
   (
                 SUM((S,T), IHOURSINST(S,T) * VGF_T.L(Y,IA,G,S,T) )$IAGK_HASORPOT(Y,IA,G)
    )$(IGNOTETOH(G) and (not IGESTO(G)) and (not IGHSTO(G)) and (not IGESTOS(G)) and (not IGHSTOS(G)))


* Electricity storages:
    +OMONEY/IOF1000000*SUM((S,T), EL_PRICE_YCRST_scenarios(scenarios,Y,C,IR,S,T,'Money_per_MWh')*VESTOLOADT.L(Y,IA,G,S,T))$IGESTO(G)
    +OMONEY/IOF1000000*SUM((S,T), EL_PRICE_YCRST_scenarios(scenarios,Y,C,IR,S,T,'Money_per_MWh')*VESTOLOADTS.L(Y,IA,G,S,T))$IGESTOS(G)
* Heat storages:
    +OMONEY/IOF1000000*SUM((S,T), H_PRICE_YCRAST_scenarios(scenarios,Y,C,IR,IA,S,T,'Money_per_MWh')*VHSTOLOADT.L(Y,IA,G,S,T))$IGHSTO(G)
    +OMONEY/IOF1000000*SUM((S,T), H_PRICE_YCRAST_scenarios(scenarios,Y,C,IR,IA,S,T,'Money_per_MWh')*VHSTOLOADTS.L(Y,IA,G,S,T))$IGHSTOS(G)
* Electricity to heat:
    +OMONEY/IOF1000000*SUM((S,T), EL_PRICE_YCRST_scenarios(scenarios,Y,C,IR,S,T,'Money_per_MWh')*VGE_T.L(Y,IA,G,S,T))$(IAGK_HASORPOT(Y,IA,G) and IGETOH(G))
;

** CO2 EMISSIONS COST
ECO_G_YCRAG_scenarios(scenarios,Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_CO2_TAX','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and IAGK_HASORPOT(Y,IA,G))=
         OMONEY/IOF1000000*IM_CO2(G)*IOF0001 * IOF3P6 *M_POL(Y,"TAX_CO2",C) * SUM((S,T), IHOURSINST(S,T)*VGF_T.L(Y,IA,G,S,T))$IAGK_HASORPOT(Y,IA,G);

** OTHER EMISSIONS COST
ECO_G_YCRAG_scenarios(scenarios,Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_OTHER_EMI_TAX','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and IAGK_HASORPOT(Y,IA,G))=
         OMONEY/IOF1000000*
         ( SUM((S,T), IHOURSINST(S,T) * (IM_SO2(G)*IOF0001) * IOF3P6 * VGF_T.L(Y,IA,G,S,T))*M_POL(Y,"TAX_SO2",C)
          + SUM((S,T), IHOURSINST(S,T) * (GDATA(G,'GDNOX')*IOF0000001) * IOF3P6 * VGF_T.L(Y,IA,G,S,T))* M_POL(Y,"TAX_NOX",C)
          )
;


** UNIT COMMITMENT COST
$ifi not %UnitComm%==yes $goto No_UC
ECO_G_YCRAG_scenarios(scenarios,Y,C,IR,IA,IGUC,FFF,TECH_TYPE,'COSTS','GENERATION_UC_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(IGUC,FFF) AND GTECH_TYPE(IGUC,TECH_TYPE) and IAGK_HASORPOT(Y,IA,IGUC))=
OMONEY/IOF1000000*SUM((S,T), IHOURSINST(S,T)*(
                                                 VUCU.L(Y,IA,IGUC,S,T)*GDATA(IGUC,'GDUCUCOST')*GDATA(IGUC,'GDUCUNITSIZE')
                                                 +VUCD.L(Y,IA,IGUC,S,T)*GDATA(IGUC,'GDUCDCOST')*GDATA(IGUC,'GDUCUNITSIZE')
                                                 +VUCON.L(Y,IA,IGUC,S,T)*GDATA(IGUC,'GDUCCOST0')*GDATA(IGUC,'GDUCUNITSIZE')
$ifi %STEPWISE_RAMPING_UC%==ramp_const_cost      +SUM(RAMPING_RATE,VRAMPING_DOWN.L(Y,IA,IGUC,RAMPING_RATE,S,T)*IRAMPING_G_COSTS_DOWN(IGUC,RAMPING_RATE)+VRAMPING_UP.L(Y,IA,IGUC,RAMPING_RATE,S,T)*IRAMPING_G_COSTS_UP(IGUC,RAMPING_RATE))
))
;
$label NO_UC


*TAXES
$ifi NOT %TAXES%==yes  $goto No_TAXES
ECO_G_YCRAG_scenarios(scenarios,Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_TAXES','Mmoney')$(CCCRRR(C,IR) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G))=
OMONEY/IOF1000000*(
* Tax(+)/subsidy(-) on fuel use for electricity generation
    + SUM((IS3,T),
         IHOURSINST(IS3,T) * TAX_F_EL(Y,C,G)  * VGE_T.L(Y,IA,G,IS3,T)/ (GDATA(G,'GDFE') * (1$(NOT GEFFRATE(IA,G))+GEFFRATE(IA,G))))

* Tax(+)/subsidy(-) on fuel use for heat generation
    + SUM((IS3,T),
         IHOURSINST(IS3,T) * TAX_F_HEAT(Y,C,G) * GDATA(G,'GDCV') * VGH_T.L(Y,IA,G,IS3,T)/ (GDATA(G,'GDFE') * (1$(NOT GEFFRATE(IA,G))+GEFFRATE(IA,G))))

* Tax(+)/subsidy(-) on fuel use
       + SUM((IS3,T),
              IHOURSINST(IS3,T) * TAX_F(Y,C,G)  * VGF_T.L(Y,IA,G,IS3,T))

* Tax(+)/subsidy(-) on heat generation
    + SUM((IS3,T),
         IHOURSINST(IS3,T) * TAX_G_HEAT(Y,C,G)  * VGH_T.L(Y,IA,G,IS3,T))

* Tax(+)/subsidy(-) on heat generation
    + SUM((IS3,T),
         IHOURSINST(IS3,T) * TAX_G_EL(Y,C,G)  * VGE_T.L(Y,IA,G,IS3,T))
)
;
$label NO_TAXES


*GRID TARIFFS
$ifi NOT %GRIDTARIFFS%==yes  $goto No_GRIDTARIFFS
ECO_G_YCRAG_scenarios(scenarios,Y,C,IR,IA,IGETOH,FFF,TECH_TYPE,'COSTS','GENERATION_GRID_TARIFFS','Mmoney')$(CCCRRR(C,IR) AND IGF(IGETOH,FFF) AND GTECH_TYPE(IGETOH,TECH_TYPE) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGETOH))=
OMONEY/IOF1000000*(

* Fixed component and subscribed power (per MW installed)
                 + IOF1000 *
                         IGR_PRICE_TECH(Y,IR,'GRDSUBS')  * (VGKNACCUMNET.L(Y,IA,IGETOH)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGETOH)$IGDECOMEXOPOT(Y,IA,IGETOH)
                          + GKFX(Y,IA,IGETOH)) /(GDATA(IGETOH,'GDFE') *(1$(NOT GEFFRATE(IA,IGETOH))+GEFFRATE(IA,IGETOH)))

* Energy charge winter
                 + SUM((IS3,T)$IGR_TIME('GRDECW',IR,IS3,T), IHOURSINST(IS3,T) * IGR_PRICE_TECH(Y,IR,'GRDECW')  * VGE_T.L(Y,IA,IGETOH,IS3,T))
* Energy charge summer
                 + SUM((IS3,T)$IGR_TIME('GRDECS',IR,IS3,T), IHOURSINST(IS3,T) * IGR_PRICE_TECH(Y,IR,'GRDECS')  * VGE_T.L(Y,IA,IGETOH,IS3,T))
* Energy charge TOU step 1
                 + SUM((IS3,T)$IGR_TIME('GRDTOU1',IR,IS3,T), IHOURSINST(IS3,T) * IGR_PRICE_TECH(Y,IR,'GRDTOU1')  * VGE_T.L(Y,IA,IGETOH,IS3,T))
* Energy charge TOU step 2
                 + SUM((IS3,T)$IGR_TIME('GRDTOU2',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_TECH(Y,IR,'GRDTOU2')  * VGE_T.L(Y,IA,IGETOH,IS3,T))
* Energy charge TOU step 3
                 + SUM((IS3,T)$IGR_TIME('GRDTOU3',IR,IS3,T), IHOURSINST(IS3,T) * IGR_PRICE_TECH(Y,IR,'GRDTOU3')  * VGE_T.L(Y,IA,IGETOH,IS3,T))
* Energy charge TOU step 4
                 + SUM((IS3,T)$IGR_TIME('GRDTOU4',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_TECH(Y,IR,'GRDTOU4')  * VGE_T.L(Y,IA,IGETOH,IS3,T))
)
;
$label NO_GRIDTARIFFS

* -- TRANSMISSION -----
*ELECTRICITY
** TRANSMISSION INVESTMENTS
ECO_X_YCR_scenarios(scenarios,Y,C,IR,'COSTS','TRANSMISSION_CAPITAL_COSTS','Mmoney')$CCCRRR(C,IR)=
          OMONEY*IOF0000001*SUM((IRI,IYALIAS2)$((IXKN(IYALIAS2,IRI,IR) OR IXKN(IYALIAS2,IR,IRI)) AND (ORD(IYALIAS2) LE ORD(Y)) AND (NOT (IXKN_ES(IR,IRI) OR IXKN_ES(IRI,IR)))) ,
                                    IOF05*ANNUITYCX(C)*IYHASANNUITYX(IYALIAS2,Y)*VXKN.L(IYALIAS2,IR,IRI)*XINVCOST(IYALIAS2,IR,IRI)
                                );
** TRANSMISSION FLOW COSTS
ECO_X_YCR_scenarios(scenarios,Y,C,IR,'COSTS','TRANSMISSION_OPERATIONAL_COSTS','Mmoney')$CCCRRR(C,IR)=
OMONEY*IOF0000001*SUM(IRI$IXK_HASORPOT(Y,IR,IRI), SUM((S,T), IHOURSINST(S,T) * (VX_T.L(Y,IR,IRI,S,T) * XCOST(IR,IRI))));

** TRANSMISSION TRADE COST
ECO_X_YCR_scenarios(scenarios,Y,C,IR,'COSTS','TRANSMISSION_TRADE_COSTS','Mmoney')$CCCRRR(C,IR)=
OMONEY*IOF0000001*SUM(IRI$IXK_HASORPOT(Y,IRI,IR), SUM((S,T), IHOURSINST(S,T) * (VX_T.L(Y,IRI,IR,S,T) * EL_PRICE_YCRST_scenarios(scenarios,Y,C,IRI,S,T,'Money_per_MWh'))));

** TRANSMISSION TRADE INCOME
ECO_X_YCR_scenarios(scenarios,Y,C,IR,'INCOME','TRANSMISSION_TRADE_INCOME','Mmoney')$CCCRRR(C,IR)=
OMONEY*IOF0000001*SUM(IRI$IXK_HASORPOT(Y,IR,IRI), SUM((S,T), IHOURSINST(S,T) * (VX_T.L(Y,IR,IRI,S,T) * EL_PRICE_YCRST_scenarios(scenarios,Y,C,IR,S,T,'Money_per_MWh'))));

*HEAT
$ifi not %HEATTRANS%==yes $goto No_HEATTRANS_costs
** TRANSMISSION INVESTMENTS
ECO_XH_YCRA_scenarios(scenarios,Y,C,IR,IA,'COSTS','HEAT_TRANSMISSION_CAPITAL_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA))=
          OMONEY*IOF0000001*SUM((IAI,IYALIAS2)$((IXHKN(IYALIAS2,IAI,IA) OR IXHKN(IYALIAS2,IA,IAI)) AND ORD(IYALIAS2) LE ORD(Y)) ,
                                    IOF05*ANNUITYCXH(C)*IYHASANNUITYXH(IYALIAS2,Y)*VXHKN.L(IYALIAS2,IA,IAI)*XHINVCOST(IYALIAS2,IA,IAI)
                                );
** TRANSMISSION FLOW
ECO_XH_YCRA_scenarios(scenarios,Y,C,IR,IA,'COSTS','HEAT_TRANSMISSION_OPERATIONAL_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA))=
OMONEY*IOF0000001*SUM(IAI$IXHK_HASORPOT(Y,IA,IAI), SUM((S,T), IHOURSINST(S,T) * (VXH_T.L(Y,IA,IAI,S,T) * XHCOST(IA,IAI))));


** TRANSMISSION TRADE COST
ECO_XH_YCRA_scenarios(scenarios,Y,C,IR,IA,'COSTS','TRANSMISSION_TRADE_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA))=
OMONEY*IOF0000001*SUM(IAI$IXHK_HASORPOT(Y,IAI,IA), SUM((S,T), IHOURSINST(S,T) * (VXH_T.L(Y,IAI,IA,S,T) * H_PRICE_YCRAST_scenarios(scenarios,Y,C,IR,IAI,S,T,'Money_per_MWh'))));

** TRANSMISSION TRADE INCOME
ECO_XH_YCRA_scenarios(scenarios,Y,C,IR,IA,'INCOME','TRANSMISSION_TRADE_INCOME','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA))=
OMONEY*IOF0000001*SUM(IAI$IXHK_HASORPOT(Y,IA,IAI), SUM((S,T), IHOURSINST(S,T) * (VXH_T.L(Y,IA,IAI,S,T) * H_PRICE_YCRAST_scenarios(scenarios,Y,C,IR,IA,S,T,'Money_per_MWh'))));


$label No_HEATTRANS_costs

* ------------- END OF COSTS --------------

* ------------- OBJECTIVE FUNCTION --------------

OBJ_YCR_scenarios(scenarios,Y,C,IR,'GENERATION_FUEL_COSTS','Mmoney')$CCCRRR(C,IR)=

*GENERATION COSTS
SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G) AND NOT((IGESTOALL(G) OR IGHSTOALL(G) OR IGETOH(G)))),
ECO_G_YCRAG_scenarios(scenarios,Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_FUEL_COSTS','Mmoney')
)
;

OBJ_YCR_scenarios(scenarios,Y,C,IR,'HYDRO_PROFILE','Mmoney')$CCCRRR(C,IR)=
OMONEY*IOF0000001*SUM((IA,IGHYRS)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,IGHYRS)), SUM((S,T), HYPPROFILS(IA,S)* IHOURSINST(S,T)
     * VGE_T.L(Y,IA,IGHYRS,S,T)))
;

OBJ_YCR_scenarios(scenarios,Y,C,IR,'GENERATION_CAPITAL_COSTS','Mmoney')$CCCRRR(C,IR)=
SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G)),
ECO_G_YCRAG_scenarios(scenarios,Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_CAPITAL_COSTS','Mmoney')
)
;

OBJ_YCR_scenarios(scenarios,Y,C,IR,'GENERATION_FIXED_COSTS','Mmoney')$CCCRRR(C,IR)=
SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G)),
ECO_G_YCRAG_scenarios(scenarios,Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_FIXED_COSTS','Mmoney')
)
;

OBJ_YCR_scenarios(scenarios,Y,C,IR,'GENERATION_OPERATIONAL_COSTS','Mmoney')$CCCRRR(C,IR)=
SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G)),
ECO_G_YCRAG_scenarios(scenarios,Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_OPERATIONAL_COSTS','Mmoney')
)
;

OBJ_YCR_scenarios(scenarios,Y,C,IR,'GENERATION_CO2_TAX','Mmoney')$CCCRRR(C,IR)=
SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G)),
ECO_G_YCRAG_scenarios(scenarios,Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_CO2_TAX','Mmoney')
)
;

OBJ_YCR_scenarios(scenarios,Y,C,IR,'GENERATION_OTHER_EMI_TAX','Mmoney')$CCCRRR(C,IR)=
SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G)),
ECO_G_YCRAG_scenarios(scenarios,Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_OTHER_EMI_TAX','Mmoney')
)
;

OBJ_YCR_scenarios(scenarios,Y,C,IR,'GENERATION_UC_COSTS','Mmoney')$CCCRRR(C,IR)=
SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G)),
ECO_G_YCRAG_scenarios(scenarios,Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_UC_COSTS','Mmoney')
)
;

OBJ_YCR_scenarios(scenarios,Y,C,IR,'TRANSMISSION_CAPITAL_COSTS','Mmoney')$CCCRRR(C,IR)=
ECO_X_YCR_scenarios(scenarios,Y,C,IR,'COSTS','TRANSMISSION_CAPITAL_COSTS','Mmoney')
;

OBJ_YCR_scenarios(scenarios,Y,C,IR,'TRANSMISSION_OPERATIONAL_COSTS','Mmoney')$CCCRRR(C,IR)=
ECO_X_YCR_scenarios(scenarios,Y,C,IR,'COSTS','TRANSMISSION_OPERATIONAL_COSTS','Mmoney')
;

$ifi not %HEATTRANS%==yes $goto No_HEATTRANS_costs_2
OBJ_YCR_scenarios(scenarios,Y,C,IR,'HEAT_TRANSMISSION_CAPITAL_COSTS','Mmoney')$CCCRRR(C,IR)=
SUM(IA$RRRAAA(IR,IA),ECO_XH_YCRA_scenarios(scenarios,Y,C,IR,IA,'COSTS','HEAT_TRANSMISSION_CAPITAL_COSTS','Mmoney'))
;

OBJ_YCR_scenarios(scenarios,Y,C,IR,'HEAT_TRANSMISSION_OPERATIONAL_COSTS','Mmoney')$CCCRRR(C,IR)=
SUM(IA$RRRAAA(IR,IA),ECO_XH_YCRA_scenarios(scenarios,Y,C,IR,IA,'COSTS','HEAT_TRANSMISSION_OPERATIONAL_COSTS','Mmoney'))
;
$label  No_HEATTRANS_costs_2


$ifi NOT %TAXES%==yes  $goto No_TAXES2
OBJ_YCR_scenarios(scenarios,Y,C,IR,'TAXES','Mmoney')$CCCRRR(C,IR)=
SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G)), ECO_G_YCRAG_scenarios(scenarios,Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_TAXES','Mmoney'))
+OMONEY/IOF1000000*(

* Tax on electricity consumption
    + SUM(DEUSER, TAX_DE(C,DEUSER) * SUM((IS3,T), IHOURSINST(IS3,T) * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0))  )

$ifi %DFLEXQUANT%==yes           + SUM((DEUSER), TAX_DE(C,DEUSER) *(
$ifi %DFLEXQUANT%==yes             SUM(DEF_U1,VDEF_T.L(Y,IR,DEUSER,IS3,T,DEF_U1) ) - SUM(DEF_D1,VDEF_T.L(Y,IR,DEUSER,IS3,T,DEF_D1) )
$ifi %DFLEXQUANT%==yes           + SUM(DEF_U2,VDEF_T.L(Y,IR,DEUSER,IS3,T,DEF_U2) ) - SUM(DEF_D2,VDEF_T.L(Y,IR,DEUSER,IS3,T,DEF_D2) )
$ifi %DFLEXQUANT%==yes           + SUM(DEF_U3,VDEF_T.L(Y,IR,DEUSER,IS3,T,DEF_U3) ) - SUM(DEF_D3,VDEF_T.L(Y,IR,DEUSER,IS3,T,DEF_D3) ))


+SUM(IA$RRRAAA(IR,IA),
* Tax on heat consumption
    + SUM(DHUSER, TAX_DH(C,DHUSER) * DH(Y,IA,DHUSER))

$ifi %DFLEXQUANT%==yes      + SUM((DHUSER), TAX_DH(C,DHUSER) * (
$ifi %DFLEXQUANT%==yes          SUM(DHF_U1,VDHF_T.L(Y,IA,DHUSER,IS3,T,DHF_U1) ) - SUM(DHF_D1,VDHF_T.L(Y,IA,DHUSER,IS3,T,DHF_D1) )
$ifi %DFLEXQUANT%==yes        + SUM(DHF_U2,VDHF_T.L(Y,IA,DHUSER,IS3,T,DHF_U2) ) - SUM(DHF_D2,VDHF_T.L(Y,IA,DHUSER,IS3,T,DHF_D2) )
$ifi %DFLEXQUANT%==yes        + SUM(DHF_U3,VDHF_T.L(Y,IA,DHUSER,IS3,T,DHF_U3) ) - SUM(DHF_D3,VDHF_T.L(Y,IA,DHUSER,IS3,T,DHF_D3) ) )
)
)
;
$label NO_TAXES2


*GRID TARIFFS2
$ifi NOT %GRIDTARIFFS%==yes  $goto No_GRIDTARIFFS2
OBJ_YCR_scenarios(scenarios,Y,C,IR,'GRID_TARIFFS','Mmoney')$CCCRRR(C,IR)=
SUM((IA,IGETOH,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,IGETOH)), ECO_G_YCRAG_scenarios(scenarios,Y,C,IR,IA,IGETOH,FFF,TECH_TYPE,'COSTS','GENERATION_GRID_TARIFFS','Mmoney'))

+OMONEY/IOF1000000*(

* Demand charge winter
                 + IOF1000 * SUM((MMM)$(SUM((IS3,T)$IGR_TIME('GRDDCW',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), SUM(IA$(SUM(IGETOH,IAGK_HASORPOT(Y,IA,IGETOH)) AND RRRAAA(IR,IA)), VGETOH_CAP1.L(Y,IA,MMM)) *  IGR_PRICE_TECH(Y,IR,'GRDDCW'))/(IGR_MONTHSINSIM/card(MMM))
* Demand charge summer
                 + IOF1000 * SUM((MMM)$(SUM((IS3,T)$IGR_TIME('GRDDCS',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), SUM(IA$(SUM(IGETOH,IAGK_HASORPOT(Y,IA,IGETOH)) AND RRRAAA(IR,IA)), VGETOH_CAP1.L(Y,IA,MMM)) *  IGR_PRICE_TECH(Y,IR,'GRDDCS'))/(IGR_MONTHSINSIM/card(MMM))
* Demand charge 2 (e.g for spring and fall months)
                 + IOF1000 * SUM((MMM)$(SUM((IS3,T)$IGR_TIME('GRDDC2',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), SUM(IA$(SUM(IGETOH,IAGK_HASORPOT(Y,IA,IGETOH)) AND RRRAAA(IR,IA)), VGETOH_CAP1.L(Y,IA,MMM)) *  IGR_PRICE_TECH(Y,IR,'GRDDC2'))/(IGR_MONTHSINSIM/card(MMM))

* Demand charge TOU
                 + IOF1000 * SUM((MMM),   SUM(IA$(SUM(IGETOH,IAGK_HASORPOT(Y,IA,IGETOH)) AND RRRAAA(IR,IA)), VGETOH_CAP2.L(Y,IA,MMM)) *  IGR_PRICE_TECH(Y,IR,'GRDDCTOU'))/(IGR_MONTHSINSIM/card(MMM))
* Demand charge yearly
                 + IOF1000 *    SUM(IA$(SUM(IGETOH,IAGK_HASORPOT(Y,IA,IGETOH)) AND RRRAAA(IR,IA)), VGETOH_CAPY.L(Y,IA)) *  IGR_PRICE_TECH(Y,IR,'GRDDCY')

* Tariffs on user groups
                 + SUM((IS3,T,DEUSER)$IGR_TIME('GRDECW',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDECW')  * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0) )
                 + SUM((IS3,T,DEUSER)$IGR_TIME('GRDECS',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDECS')  * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0) )
                 + SUM((IS3,T,DEUSER)$IGR_TIME('GRDTOU1',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDTOU1') * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0) )
                 + SUM((IS3,T,DEUSER)$IGR_TIME('GRDTOU2',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDTOU2') * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0) )
                 + SUM((IS3,T,DEUSER)$IGR_TIME('GRDTOU3',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDTOU3') * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0) )
                 + SUM((IS3,T,DEUSER)$IGR_TIME('GRDTOU4',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDTOU4') * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0) )

                 + IOF1000 * SUM((DEUSER,MMM)$(SUM((IS3,T)$IGR_TIME('GRDDCW',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), VDEUSER_CAP1.L(Y,IR,DEUSER,MMM) *  IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDDCW'))/(IGR_MONTHSINSIM/CARD(MMM))
                 + IOF1000 * SUM((DEUSER,MMM)$(SUM((IS3,T)$IGR_TIME('GRDDCS',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), VDEUSER_CAP1.L(Y,IR,DEUSER,MMM) *  IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDDCS'))/(IGR_MONTHSINSIM/CARD(MMM))
                 + IOF1000 * SUM((DEUSER,MMM)$(SUM((IS3,T)$IGR_TIME('GRDDC2',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), VDEUSER_CAP1.L(Y,IR,DEUSER,MMM) *  IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDDC2'))/(IGR_MONTHSINSIM/CARD(MMM))
                 + IOF1000 * SUM((DEUSER,MMM),   VDEUSER_CAP2.L(Y,IR,DEUSER,MMM) *  IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDDCTOU'))/(IGR_MONTHSINSIM/card(MMM))
                 + IOF1000 * SUM((DEUSER),  VDEUSER_CAPY.L(Y,IR,DEUSER) *  IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDDCY'))

)


;
$label NO_GRIDTARIFFS2

*ADD OTHER COSTS(HYDROGEN,ETC)


* ------------- END OF OBJECTIVE FUNCTION --------------


* ------------- INCOME --------------

** ELECTRICITY SALES
ECO_G_YCRAG_scenarios(scenarios,Y,C,IR,IA,IGE,FFF,TECH_TYPE,'INCOME','ELECTRICITY_SALE','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) and IGNOTETOH(IGE))=
 OMONEY*IOF0000001*
   SUM((S,T), IHOURSINST(S,T)*VGE_T.L(Y,IA,IGE,S,T)*EL_PRICE_YCRST_scenarios(scenarios,Y,C,IR,S,T,'Money_per_MWh'))
;

** HEAT SALES
ECO_G_YCRAG_scenarios(scenarios,Y,C,IR,IA,IGH,FFF,TECH_TYPE,'INCOME','HEAT_SALE','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE))=
 OMONEY*IOF0000001*
   SUM((S,T), IHOURSINST(S,T)*VGH_T.L(Y,IA,IGH,S,T)*H_PRICE_YCRAST_scenarios(scenarios,Y,C,IR,IA,S,T,'Money_per_MWh'))
;

* ------------- END OF INCOME --------------

* ------------- REVENUE --------------
ECO_G_YCRAG_scenarios(scenarios,Y,C,IR,IA,G,FFF,TECH_TYPE,'REVENUE','TOTAL_REVENUE','Mmoney')$IAGK_HASORPOT(Y,IA,G)=
SUM(SUBCATEGORY, ECO_G_YCRAG_scenarios(scenarios,Y,C,IR,IA,G,FFF,TECH_TYPE,'INCOME',SUBCATEGORY,'Mmoney')-ECO_G_YCRAG_scenarios(scenarios,Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS',SUBCATEGORY,'Mmoney')
)
;

ECO_G_YCRAG_scenarios(scenarios,Y,C,IR,IA,G,FFF,TECH_TYPE,'REVENUE','ENERGY_SPECIFIC_REVENUE','Money_per_MWh')$(IAGK_HASORPOT(Y,IA,G) AND SUM(COMMODITY,PRO_YCRAGF_scenarios(scenarios,Y,C,IR,IA,G,FFF,COMMODITY,TECH_TYPE,'TWh') > 0)) =
ECO_G_YCRAG_scenarios(scenarios,Y,C,IR,IA,G,FFF,TECH_TYPE,'REVENUE','TOTAL_REVENUE','Mmoney')/SUM(COMMODITY,PRO_YCRAGF_scenarios(scenarios,Y,C,IR,IA,G,FFF,COMMODITY,TECH_TYPE,'TWh'))
;
* ------------- END OF REVENUE --------------

* --------- END OF ECONOMIC OUTPUT -----------

* ---------------------- ENVIRONMENT ----------------------

* ------------- CO2 EMISSIONS -------------

EMI_YCRAG_scenarios(scenarios,Y,C,IR,IA,G,FFF,TECH_TYPE,'kton')$(F_CONS_YCRA_scenarios(scenarios,Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh'))=
F_CONS_YCRA_scenarios(scenarios,Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh')*IM_CO2(G)*IOF3P6
;

* ------------- END OF CO2 EMISSIONS -------------

* ---------------------- END OF ENVIRONMENT ----------------------

